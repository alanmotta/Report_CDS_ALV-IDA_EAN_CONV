*&---------------------------------------------------------------------*
*& Include          ZIMM_LOG_L4L_NBDS_VS_POS_F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*& Form f_exibe_alv
*&---------------------------------------------------------------------*
FORM f_exibe_alv .

  DATA(o_ida) = cl_salv_gui_table_ida=>create_for_cds_view( iv_cds_view_name = 'ZCDS_LOG_L4L_NBDS_VS_PO' ).

  DATA(o_sel) = NEW cl_salv_range_tab_collector( ).
  o_sel->add_ranges_for_name( iv_name = 'EBELN'     it_ranges = s_ebeln[] ).
  o_sel->add_ranges_for_name( iv_name = 'DATEFILE'  it_ranges = s_dtfile[] ).
  o_sel->add_ranges_for_name( iv_name = 'LIFNR'     it_ranges = s_lifnr[] ).
  o_sel->add_ranges_for_name( iv_name = 'EBELN_PR'  it_ranges = s_doc_pr[] ).
  o_sel->add_ranges_for_name( iv_name = 'CRIADO'    it_ranges = s_criado[] ).
  o_sel->add_ranges_for_name( iv_name = 'EAN11'     it_ranges = r_ean11[] ).
  o_sel->get_collected_ranges( IMPORTING et_named_ranges = DATA(lt_sel_crit) ).
  o_ida->set_select_options( it_ranges = lt_sel_crit ).

  "Adjust column names
  TRY.
      o_ida->field_catalog( )->set_field_header_texts(
        EXPORTING
          iv_field_name        = 'EBELN'
          iv_header_text       = 'NBDS Nike'
          iv_tooltip_text      = 'NBDS Nike'
          iv_tooltip_text_long = 'NBDS Nike'
      ).
    CATCH cx_salv_ida_unknown_name.       " IDA API contract violated by caller
    CATCH cx_salv_call_after_1st_display. " Method call not allowed after first display
  ENDTRY.

  TRY.
      o_ida->field_catalog( )->set_field_header_texts(
        EXPORTING
          iv_field_name        = 'DATEFILE'
          iv_header_text       = 'Data de Integração'
          iv_tooltip_text      = 'Data de Integração'
          iv_tooltip_text_long = 'Data de Integração'
      ).
    CATCH cx_salv_ida_unknown_name.       " IDA API contract violated by caller
    CATCH cx_salv_call_after_1st_display. " Method call not allowed after first display
  ENDTRY.

  "Turn on text search feature
  o_ida->standard_functions( )->set_text_search_active( abap_true ).
  o_ida->field_catalog( )->enable_text_search( 'LOG' ).

  "Show IDA
  o_ida->fullscreen( )->display( ).
ENDFORM.
*&---------------------------------------------------------------------*
*& Form f_check_parametros
*&---------------------------------------------------------------------*
FORM f_check_parametros .

  IF s_ebeln-low  IS INITIAL AND
     s_dtfile-low IS INITIAL AND
     s_doc_pr-low IS INITIAL AND
     s_lifnr-low  IS INITIAL AND
     s_criado-low IS INITIAL AND
     s_matnr-low  IS INITIAL.

    MESSAGE 'Favor inserir filtro para seleção' TYPE 'S' DISPLAY LIKE 'E'.
    LEAVE LIST-PROCESSING.
  ENDIF.
ENDFORM.
*&---------------------------------------------------------------------*
*& Form f_busca_dados
*&---------------------------------------------------------------------*
FORM f_busca_dados .

  IF s_matnr-low IS NOT INITIAL.

    SELECT matnr, ean11, satnr
      FROM mara
      INTO TABLE @DATA(lt_mara)
      WHERE satnr IN @s_matnr.

    IF sy-subrc IS INITIAL.
      SORT lt_mara BY ean11.

      r_ean11 = VALUE #( FOR ls_mara IN  lt_mara
                          LET s = 'I'
                              o = 'E' IN
                              sign   = s
                              option = o
                            ( low = ls_mara-ean11 )
                       ).
    ELSE.
      MESSAGE 'Material indicado não é Pai' TYPE 'S' DISPLAY LIKE 'E'.
      LEAVE LIST-PROCESSING.
    ENDIF.
  ENDIF.
ENDFORM.
